<!DOCTYPE html>
<meta charset="utf-8">

<link href="bootstrap/css/bootstrap.min.css" rel="stylesheet">
<link rel="stylesheet" type="text/css" href="css/daterangepicker.css" />

<style>

body {
  overflow-y:scroll;
}

text {
  font: 12px sans-serif;
}

</style>
<body>

  <div class="row">
    <div class="span2">
      <h3>Date Range</h3>
    </div>
    <div class="span8">
      <span class="add-on"><i class="icon-calendar"></i></span>
      <input type="text" name="daterange" />
    </div>
  </div>

  <div class="row">
    <div class="span2">
      <h3>Temperature</h3>
      </div>
    <div class="span8">
      <div id="temperature" style="width:700px;height:300px;"></div>
    </div>
  </div>

  <div class="row">
    <div class="span2">
      <h3>Dehumidifier</h3>
      </div>
    <div class="span8">
      <div id="dehumidifier" style="width:700px;height:50px;"></div>
    </div>
  </div>

  <div class="row">
    <div class="span2">
      <h3>Humidity</h3>
      </div>
    <div class="span8">
      <div id="humidity" style="width:700px;height:300px;"></div>
    </div>
  </div>

  <div class="row">
    <div class="span2">
      <h3>Dew Point</h3>
      </div>
    <div class="span8">
      <div id="dew_point" style="width:700px;height:300px;"></div>
    </div>
  </div>


<script src="lib/jquery-1.8.1.min.js"></script>
<script src="lib/jquery.flot.js"></script>
<script src="lib/jquery.flot.time.js"></script>
<script src="lib/coffee-script.js"></script>
<script src="bootstrap/js/bootstrap.min.js"></script>
<script type="text/javascript" src="lib/date.js"></script>
<script type="text/javascript" src="lib/daterangepicker.js"></script>
<script type="text/coffeescript">
# some "constants"
SENSORS = [ "Lounge Room", "Kitchen", "Outside", "Study Sill" ] # sensor locations
DAY=60*60*24*1000 # one day in ms

# Data types
class Sensor
        constructor: (@index, temp, humidity) ->
                @temp = parseFloat temp
                @humidity = parseFloat humidity
                @dew_point = @temp - (100-@humidity)/5

        isvalid: ->
                (not isNaN(@temp)) and @humidity > 1

class Sample
        constructor: (csv_line) ->
                [ts,h0,t0,h1,t1,h2,t2,h3,t3,num_updates,dehumidifier] = csv_line.split(",")
                @sensors = [ new Sensor(0,t0,h0), new Sensor(1,t1,h1), new Sensor(2,t2,h2), new Sensor(3,t3,h3) ]
                @dehumidifier = dehumidifier == "1"
                @raw_timestamp = ts*1000 # ms since epoch
                @timestamp = new Date(@raw_timestamp)
                # across all the sensor dew points, choose the maximum dew point value
                @max_dew_point = (s.dew_point for s in @sensors).reduce ((x,y) -> Math.max(x,y)), 0

        isvalid: ->
                # some invalid data can creep into the CSV files, use this function to filter it out
                invalids = (s for s in @sensors when not (s? and s.isvalid()))
                invalids.length == 0


fetch_chart_data = (from,to) ->
        # determine the file paths for all CSV files (1 UTC day each) in the viewing range, start AJAX downloads
        #
        from = new Date(from.getFullYear(), from.getMonth(), from.getDate()) # normalise from 00:00:00
        to = new Date(to.getFullYear(), to.getMonth(), to.getDate(), 23, 59, 59) # normalise to 23:59:59
        days = (new Date(d) for d in [from.getTime()..to.getTime()+DAY-1] by DAY)

        # hand-rolled function to format the date:
        pad_length = (x,len) ->
                if x.toString().length < len then pad_length("0"+x,len) else x
        format_filename = (d) -> "data/" + d.getUTCFullYear() + pad_length(d.getUTCMonth()+1,2) + pad_length(d.getUTCDate(),2) + ".csv"

        filenames = (format_filename(d) for d in days)
        fetched_data = (undefined for d in days) # placeholder to fill the data for each day

        get_callback = (day_index) -> (data) ->
                fetched_data[day_index] = data.split "\n"
                if (s for s in fetched_data when s?).length == fetched_data.length # all days fetched
                        all_data_fetched(from, to, fetched_data)
        error_callback = (day_index) -> () ->
                        get_callback(day_index)("")
        ($.get(url, get_callback(index)).error(error_callback index) for url,index in filenames)

all_data_fetched = (from,to,fetched_data) ->
        # once all CSV files are available, load into Sample objects & generate graphs
        lines = [].concat fetched_data...
        samples = (new Sample(line) for line in lines)
        samples = (s for s in samples when s.timestamp > from and s.timestamp < to)
        plot_graphs extract_graph_data((s for s in samples when s? and s.isvalid())), from, to

extract_graph_data = (samples) ->
        # Rearrange the individual samples into data objects suitable for use in flot
        #
        # temp,humidity, dewpoint axes are generated by extracting a particular field
        # from a particular sensor set
        extract_axes = (sensor_index, field_fn) ->
                ([sample.raw_timestamp, field_fn(sample.sensors[sensor_index])] for sample in samples)
        temp_data = (i) ->
                data: extract_axes(i,(sample)->sample.temp)
                label: SENSORS[i]
                hoverable: true
        humidity_data = (i) ->
                data: extract_axes(i,(sample)->sample.humidity)
                label: SENSORS[i]
                hoverable: true
        dew_point_data = (i) ->
                data: extract_axes(i,(sample)->sample.dew_point)
                label: SENSORS[i]
                hoverable: true

        # max dewpoint & dehumidifier on/off work differently
        max_dew_point_data =
                data: ([d.raw_timestamp, d.max_dew_point] for d in samples)
                label: "Max Dewpoint"
                hoverable: true
        dehumidifier_data =
                data: ([d.raw_timestamp, 0+d.dehumidifier] for d in samples)
                label: "Dehumidifier Setting"
                hoverable: true
                color: "6EF0FF"

        range = [0..SENSORS.length-1]
        # pack the data into an object to be passed to plot_graphs
        dataset =
            temp: (temp_data(i) for i in range).concat(max_dew_point_data)
            humidity: (humidity_data(i) for i in range)
            dew_point: (dew_point_data(i) for i in range)
            dehumidifier: [dehumidifier_data]

plot_graphs = (dataset, from, to) ->
        # plot the data sets onto individual charts in flot
        xaxis =
            min: from,
            max: Math.min(to, new Date()),
            mode: 'time',
            timezone: 'browser'
        yaxis =
            min: 0
        metadata =
            xaxes: [ xaxis ]
            yaxes: [ yaxis ]
            hoverable: true
            legend: { position: 'sw', backgroundOpacity: 0.3 }
            grid: { hoverable: true }
        $.plot $("#temperature"), dataset.temp,  metadata
        $.plot $("#humidity"), dataset.humidity,  metadata
        $.plot $("#dew_point"), dataset.dew_point,  metadata

        # dehumidifier chart has distinct axes for Off/On (not a real line graph)
        dehumidifier_metadata = $.extend {}, metadata
        dehumidifier_metadata.yaxes[0].max = 1
        dehumidifier_metadata.legend.show = false
        dehumidifier_metadata.yaxes[0].ticks = [[0,"Off"],[1,"On"]]
        $.plot $("#dehumidifier"), dataset.dehumidifier, dehumidifier_metadata

# Showing/hiding the mouseover tooltip

previousPoint = null
event_plothover = (event, pos, item) ->
        window.clearTimeout $("#tooltip").data("timeout")
        if (not item) or previousPoint == item.dataIndex
                graph_hide()
        else
                previousPoint = item.dataIndex;
                $("#tooltip").remove()
                x = item.datapoint[0].toFixed(2)
                y = item.datapoint[1].toFixed(2)
                ts = new Date(parseInt(x)).toString("d-MMM hh:mm tt")
                show_tooltip item.pageX, item.pageY, item.series.label + " at " + ts + " = " + y

graph_hide = () ->
        $("#tooltip").remove();
        previousPoint = null;

show_tooltip = (x, y, contents) ->
        $('<div id="tooltip">' + contents + '</div>').css( {
            position: 'absolute',
            display: 'none',
            top: y + 5,
            left: x + 5,
            border: '1px solid #fdd',
            padding: '2px',
            'background-color': '#fee',
            opacity: 0.80
        }).appendTo("body").fadeIn(200)

# ready function

$(document).ready () ->
       ranges =
            'Today': ['today', 'today'],
            'Yesterday': ['yesterday', 'yesterday'],
            'Last 7 Days': [Date.today().add({ days: -6 }), 'today'],
            'Last 30 Days': [Date.today().add({ days: -29 }), 'today'],
        params =
            ranges: ranges
        callback = (start,end) ->
        $('input[name="daterange"]')
            .daterangepicker(params, fetch_chart_data)
            .val(Date.today().toString("MM/dd/yyyy - MM/dd/yyyy"))
        fetch_chart_data new Date(), new Date()

        $(id).bind "plothover", event_plothover for id in [ "#temperature", "#humidity", "#dew_point", "#dehumidifier" ]

</script>
</body>
</html>

