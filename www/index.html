<!DOCTYPE html>
<meta charset="utf-8">

<link href="bootstrap/css/bootstrap.min.css" rel="stylesheet">
<link rel="stylesheet" type="text/css" href="css/daterangepicker.css" />

<style>

body {
  overflow-y:scroll;
}

text {
  font: 12px sans-serif;
}

</style>
<body>

  <div class="container">
    <div class="row">
      <p><form class="form-horizontal">
          <label class="control-label" for="reservation">Date Range:</label>
          <div class="controls">
            <div class="input-prepend">
              <span class="add-on"><i class="icon-calendar"></i></span>
              <input type="text" name="daterange" id="daterange" />
            </div>
          </div>
        </form>
      </p>
    </div>

    <div id="graph" style="width:800px;height:500px;"></div>

    <div class="row" id="chart1">
      <p><svg style="height: 500px;"></svg></p>
    </div>
  </div>

<script src="lib/jquery-1.8.1.min.js"></script>
<script src="lib/jquery.flot.js"></script>
<script src="lib/coffee-script.js"></script>
<script src="bootstrap/js/bootstrap.min.js"></script>
<script type="text/javascript" src="lib/date.js"></script>
<script type="text/javascript" src="lib/daterangepicker.js"></script>
<script type="text/coffeescript">
# some "constants"
SENSORS = [ "Lounge Room", "Kitchen", "Outside", "Study Sill" ] # sensor locations
TEMP_COLOURS =     [ "#A2285E", "#8C003D", "#D8005F", "#EB3B88" ]
HUMIDITY_COLOURS = [ "#472B83", "#290671", "#7546D7", "#4512AE" ]
DAY=60*60*24*1000 # one day in ms

class Sensor
        constructor: (@index, temp, humidity) ->
                @temp = parseFloat temp
                @humidity = parseFloat humidity

        isvalid: ->
                not (isNaN(@temp) or isNaN(@humidity))

class Sample
        constructor: (timestamp, @sensors) ->
                @timestamp = new Date(timestamp*1000)

        isvalid: ->
                invalids = (s for s in @sensors when not (s? and s.isvalid()))
                invalids.length == 0

fetch_chart_data = (from,to) ->
        from = new Date(from.getFullYear(), from.getMonth(), from.getDate()) # normalise from 00:00:00
        to = new Date(to.getFullYear(), to.getMonth(), to.getDate(), 23, 59, 59) # normalise to 23:59:59
        days = (new Date(d) for d in [from.getTime()..to.getTime()+DAY-1] by DAY)
        # no good date formatter loaded(!)
        pad_length = (x,len) ->
                if x.toString().length < len then pad_length("0"+x,len) else x
        format_filename = (d) -> "data/" + d.getUTCFullYear() + pad_length(d.getUTCMonth()+1,2) + pad_length(d.getUTCDate(),2) + ".csv"
        filenames = (format_filename(d) for d in days)
        fetched_data = (undefined for d in days) # placeholder to fill the data for each day

        get_callback = (day_index) -> (data) ->
                fetched_data[day_index] = data.split "\n"
                if (s for s in fetched_data when s?).length == fetched_data.length # all days fetched
                        lines = [].concat fetched_data...
                        samples = (build_sample line.split(",")... for line in lines)
                        samples = (s for s in samples when s.timestamp > from and s.timestamp < to)
                        load_graph generate_datasets((s for s in samples when s? and s.isvalid())), from, to
        ($.get url, get_callback(index) for url,index in filenames)


day = new Date(2012,8,4)
fetch_chart_data day, day

build_sample = (ts,h0,t0,h1,t1,h2,t2,h3,t3,num_updates,out_status) ->
        sensors = [ new Sensor(0,t0,h0), new Sensor(1,t1,h1), new Sensor(2,t2,h2), new Sensor(3,t3,h3) ]
        new Sample(parseInt(ts), sensors)

generate_datasets = (samples) ->
        temp_data = (i) ->
                data: generate_sensordata(samples,i,(i)->i.temp)
                label: "Temp "+SENSORS[i]
                color: TEMP_COLOURS[i]
        humidity_data = (i) ->
                data: generate_sensordata(samples,i,(i)->i.humidity)
                label: "Humidity "+SENSORS[i]
                color: HUMIDITY_COLOURS[i]
                yaxis: 2
        range = [0..SENSORS.length-1]
        (temp_data(i) for i in range).concat(humidity_data(i) for i in range)

generate_sensordata = (samples, sensor_index, field_fn) ->
        ([sample.timestamp.getTime(), field_fn(sample.sensors[sensor_index])] for sample in samples)

load_graph = (data, from, to) ->
           $.plot($("#graph"), data,  {
                               xaxes: [ { mode: 'time', timezone: 'browser' } ],
                               yaxes: [ { min: 0 },
                                   {
                                       alignTicksWithAxis: true,
                                       position: "right",
                                   } ],
                               legend: { position: 'sw' }
           })

$(document).ready () ->
        ranges =
            'Today': ['today', 'today'],
            'Yesterday': ['yesterday', 'yesterday'],
            'Last 7 Days': [Date.today().add({ days: -6 }), 'today'],
            'Last 30 Days': [Date.today().add({ days: -29 }), 'today'],
        params =
            ranges: ranges
        callback = (start,end) ->
            fetch_chart_data start, end
        $('input[name="daterange"]').daterangepicker params, callback

</script>
